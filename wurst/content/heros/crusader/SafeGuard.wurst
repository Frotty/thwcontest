package SafeGuard

import Hero
import ClosureEvents
import ClosureTimers
import DamageMod
import Damager
import Lightning
import AbilityTooltipGenerator

public int SAFEGUARD_SPELL_ID

constant SAFEGUARD_NAME = "Safe Guard"
constant SAFEGUARD_HOTKEY = "E"
constant SAFEGUARD_MAXLVL = 4
constant SAFEGUARD_DESCR = "Shields an allied hero, and transmits all the damage it takes to the crusader.|n At the end of the effect the crusader heals for percentage of the taken damage. If crusader leaves the target's side, the effect ends."

function getSafeGuardDuration(int lvl) returns real
	return 5. + 1 * lvl
	
function getSafeGuardHealPercent(int lvl) returns real
	return 50. + 0.125 * lvl

function getSafeGuardCooldown(int lvl) returns int
	return 26 - 3 * lvl

public class SafeGuardCast
	Hero caster
	real sg_damage // saves incoming damage for healback
	texttag sg_tt
	
	construct(Hero cast, unit target, int lvl)
		caster = cast
		castSafeGuard(target, lvl)

	//OnDamage onDamageClosure
	
	function castSafeGuard(unit target, int lvl) // E ability of Crusader. Allied hero cast.
		// Should probably work on summouns (of there every happen any) too
		let targetEntity = target.getEntity()
		if target == caster.actor //Different effect if casted on self.
			print("yuo cheating!")
		else
			print("created")
			// Create local vars to be captured by closure (ignore warning)
			let speed = 600.
			let zoffset = vec3(0,0,50) // Lightning height offset
			caster.setTarget(targetEntity.pos.toVec2(),speed)
			var jump_duration = caster.pos.distToVec2d(targetEntity.pos) / speed //Using the same formula as the setTarget() function here.
			if jump_duration < 1.
				jump_duration = 1./speed
			jump_duration *= ANIMATION_PERIOD
			doAfter(jump_duration, () -> begin //We only add the effects of the ability when the jump is done.
				let maxDistance = 300
				let lng = addLightning("SPLK", true, caster.pos+zoffset, targetEntity.pos+zoffset)
				let eff = AddSpecialEffectTarget("Models\\DivineBarrier1.mdx",targetEntity.actor,"origin")
				sg_tt = createTTEx(target.getPos().withZ(150), "", 11, colorA(122,200,255,255))

				OnDamage ond = (unit damaged, unit source, real amount) -> begin
					let dmg = getDamage() //* 0.75
					modifyDamage(0)//(getDamage() * 0.25)
					sg_damage += dmg
					source.damageTarget(caster.actor, dmg)
					var col = lng.getColor()
					if col.green > 150
						col.green = 2
						col.blue = 2
					sg_tt.setText(sg_damage.toInt().toString(),11)
					lng.setColor(col)
					AddSpecialEffectTarget("Abilities\\Spells\\Human\\Feedback\\SpellBreakerAttack.mdl",targetEntity.actor,"origin").destr()
					AddSpecialEffectTarget("Abilities\\Spells\\Human\\Feedback\\SpellBreakerAttack.mdl",caster.actor,"origin").destr()
				end
				
				onDamage(target, ond)
				doPeriodicallyTimed(ANIMATION_PERIOD, getSafeGuardDuration(lvl), (CallbackCounted cb) -> begin
					var col = lng.getColor()
					col.alpha = ((maxDistance - (caster.pos.distToVec(targetEntity.pos) / maxDistance)) * 255).toInt()
					if col.blue < 255
						col.blue = (col.blue*1.3).toInt()
					if col.green < 255
						col.green = (1.3*col.green).toInt()
					lng..move(true, caster.pos+zoffset, targetEntity.pos+zoffset)
					..setColor(col)
					if cb.isLast() or caster.pos.distToVec(targetEntity.pos) > maxDistance
						lng.destr()
						eff.destr()
						caster.actor.setHP(caster.actor.getHP() + sg_damage * getSafeGuardHealPercent(lvl))
						sg_damage = 0
						AddSpecialEffectTarget("Abilities\\Spells\\Human\\HolyBolt\\HolyBoltSpecialArt.mdl",caster.actor,"origin").destr()
						sg_tt.destr()
						destroy ond
						destroy this
				end)
			end)

public function init_SafeGuard(boolean compileTime)
	SAFEGUARD_SPELL_ID = ABIL_ID_GEN.next()
	
	if compileTime
		var safeguard = new ChannelAbilityPreset(SAFEGUARD_SPELL_ID, 4)
		// Set Attributes
		safeguard..removeChannelProperties(true)..setName("Safeguard")
			..setButtonPositionNormalX(2)..setButtonPositionNormalY(2)
			..setButtonPositionResearchX(2)..setButtonPositionResearchY(0)
			..setIcon("BTNSpell_Holy_HolyNova")..setOption(Option.VISIBLE, true)
			..setCastRange(1,400)..setTargetType(Targettype.UTARGET)
			..setTargetsAllowed(1,"friend")
		new AbilityTooltipGenerator(SAFEGUARD_NAME, SAFEGUARD_HOTKEY, Targettype.UTARGET, SAFEGUARD_MAXLVL, SAFEGUARD_DESCR)
		..addProperty("Cast Range:", (400.).toString())
		..addProperty("Duration:", (int lvl) -> getSafeGuardDuration(lvl).toString())
		..addProperty("Cooldown:", (int lvl) -> getSafeGuardCooldown(lvl).toString())
		..addProperty("Heal Damage%:", (int lvl) -> (getSafeGuardHealPercent(lvl)*100).toInt().toString())
		..applyToDef(safeguard)
		