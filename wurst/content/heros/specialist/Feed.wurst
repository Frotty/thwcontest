package Feed

import MapStuff
import ChannelAbilityPreset
import UnitObjEditing
import Projectile
import Creep
import DamageMod
//import Damager
import ClosureEvents

public int FEED_SPELL_ID
int FOOD_DUMMY_ID

string foodProjectile_fx = "Models\\Ham.mdl"
real foodProjectile_radius = 90.
real friction = 0.9

public class FoodProjectile extends Projectile
	unit dummyUnit = null
	boolean arrived = false
	trigger range_trigger
	texttag tt = null
	
	construct( vec3 pos, player owner,real dist, angle xyAngle )
		super( pos, foodProjectile_radius, owner, xyAngle, foodProjectile_fx )
		print("Created Food Projectile. Is it Flying?")
		//Do the same as setTarget
		setSpeed((1/friction - 1)*dist / ANIMATION_PERIOD)
	
	override function update()
		super.update()
		if getSpeed() < 20
			if not arrived 
				setSpeed(0) 
				arrived = true
				dummyUnit = createUnit(CREEP_PLAYER,FOOD_DUMMY_ID,pos,angle(0))
				dummyUnit..addAbility('Agho')..setUserData(this castTo int)
						
					
				range_trigger = CreateTrigger()
				range_trigger.registerUnitInRangeSource(dummyUnit,400, null)
				range_trigger.addAction(function onFoodRange)
				tt = createTTEx(pos + vec3(0,0,100), "Ham", 10, colorA(250,80,80,210))
		else
			setSpeed(getSpeed()*friction)
			
	function onInRange(unit target)
		Entity e = target.getEntity()
		if e instanceof Creep
			var c = e castTo Creep
			c.actor.issueTargetOrder("attack",dummyUnit)
			print("Yarr! Seems delicious")
		
	static function onFoodRange()
		unit source = GetTriggeringTrigger().getSource()
		unit u = GetTriggerUnit()
		FoodProjectile f = source.getUserData() castTo FoodProjectile
		f.onInRange(u)
		
	ondestroy
		dummyUnit.remove()
		range_trigger.destr()
		tt.destr()
	

public function init_Feed(boolean compileTime)

	FEED_SPELL_ID = ABIL_ID_GEN.next()
	if compileTime
	// Generate def if compieltime
		var feed = new ChannelAbilityPreset(FEED_SPELL_ID, 4)
		// Set Attributes
		feed..removeChannelProperties(true)..setName("Feed")
			..setButtonPositionNormalX(0)..setButtonPositionNormalY(2)
			..setButtonPositionResearchX(0)..setButtonPositionResearchY(0)
			..setIcon("BTNHolyBolt")..setOption(Option.VISIBLE, true)
			..setTargetType(Targettype.PUTARGET)
			..setCastRange(1,400)
			..setOrderStringActivate("warstomp")
			
	FOOD_DUMMY_ID = UNIT_ID_GEN.next()
	if compileTime
		var def = new UnitDefinition(FOOD_DUMMY_ID,'otot')
			..setHitPointsMaximumBase(9000)
			..setModelFile("Models\\Ham.mdl") // Laptop keyboard sux!!! (keeping this for future)
			..setCollisionSize(0) //Add invisibility for players laterper trigger
