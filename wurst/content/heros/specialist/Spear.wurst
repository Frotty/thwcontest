package Spear

import MapStuff
import ChannelAbilityPreset
import Projectile
import LinkedList
import Creep
import CollisionDetection
import ClosureTimers
import Terrain
import StunHandler

public int SPEAR_SPELL_ID

constant SPEAR_FX = "abilities\\weapons\\WyvernSpear\\WyvernSpearMissile.mdl"

function getSpearDamage(int lvl) returns real
	return 10. + 10 * lvl*lvl
	
function getSpearTargetCount(int lvl) returns int
	return 1 + 2 * lvl*lvl

function getSpearSpeed(int lvl) returns real
	return (200. + 50 * lvl)
	
function getSpearRange(int lvl) returns real
	return 600. + 200 * lvl
	
public class Spear extends Projectile
	LinkedList<Creep> targets
	LinkedList<int> lastCount
	
	construct( vec3 pos, player owner, angle xyAngle, int lvl )
		super(pos, owner, xyAngle, SPEAR_FX )
		//Creation
		targets = new LinkedList<Creep> ()
		lastCount = new LinkedList<int> ()
		
		setRanged(getSpearRange(lvl))
		setSpeed(getSpearSpeed(lvl))
		
		CallbackCounted cb = (CallbackCounted cb) -> begin
			//Loop trough the targets here, add the targets later in the onHitCallback
			//Using integer, so we can loop trough both linkedlists at the same time
			for i=0 to targets.getSize()-1
				if cb.getCount() > lastCount.get(i)
					var p = fx.getPos2().polarOffset(xyAngle,i * 10.)
					targets.get(i).setPos(p.toVec3(), getTerrainZ(p))
				else if cb.getCount() == lastCount.get(i) or targets.get(i).actor.isAliveTrick()
					targets.get(i).addVel(vec2(0,0).polarOffset(xyAngle,getSpearSpeed(lvl)).toVec3())
					targets.removeAt(i)
			if cb.isLast()
				destroy this
		end
		let period = ANIMATION_PERIOD
		doPeriodicallyTimed(period, getSpearRange(lvl)/getSpearSpeed(lvl), cb)
		
		fx.getDummy().registerOnHitCallback(32, true, (unit hit, unit hitter) -> begin
			if fx.getDummy() == hitter
				if hit.getEntity() instanceof Creep and hit.isAliveTrick()
					var noteffected = true
					//Checking if not targeted here
					for t in targets
						if t.actor == hit
							noteffected = false
					if noteffected
					var c = hit.getEntity() castTo Creep
					fx.getDummy().damageTarget(hit, getSpearDamage(lvl))
					// Todo MASS handling
					if targets.getSize() < getSpearTargetCount(lvl)
						let w = c.weightFactor
						let travelduration = (getSpearRange(lvl)/getSpearSpeed(lvl)*w*w*w)
						//If everything fine, then add.
						targets.add(c)
						hit..applyStun(false, travelduration, c.owner)
						lastCount.add(cb.getCount()-(travelduration/period).toInt())
		end)
		
		
	ondestroy
		fx.getDummy().removeOnHitCallback()
		destroy targets
		destroy lastCount



public function init_Spear(boolean compileTime)
	SPEAR_SPELL_ID = ABIL_ID_GEN.next()
	
	if compileTime
		var def = new ChannelAbilityPreset(SPEAR_SPELL_ID, 4)
		// Set Attributes
		def..removeChannelProperties(true)..setName("Sausage Spear")
			..setButtonPositionNormalX(1)..setButtonPositionNormalY(2)
			..setButtonPositionResearchX(1)..setButtonPositionResearchY(0)
			..setIcon("BTNThoriumRanged")..setOption(Option.VISIBLE, true)
			..setTargetType(Targettype.PUTARGET)
			..setCastRange(1,1000)
			..setOrderStringActivate("starfall")