package Spear
import HashMap
import MapStuff
import ChannelAbilityPreset
import Projectile
import LinkedList
import Creep
import CollisionDetection
import ClosureTimers
import Terrain
import StunHandler
import Interpolation

public int SPEAR_SPELL_ID

constant SPEAR_FX = "abilities\\weapons\\WyvernSpear\\WyvernSpearMissile.mdl"

function getSpearDamage(int lvl) returns real
	return 10. + 10 * lvl*lvl
	
function getSpearTargetCount(int lvl) returns int
	return 1 + 2 * lvl*lvl

function getSpearSpeed(int lvl) returns real
	return (200. + 50 * lvl)
	
function getSpearRange(int lvl) returns real
	return 600. + 200 * lvl

class SpearCreep
	Creep creep
	real durPercent = 1.
	vec2 offset
	
	construct(Creep c, vec2 offset)
		creep = c
		this.offset = offset
	
public class Spear extends Projectile
	LinkedList<SpearCreep> targets = new LinkedList<SpearCreep>()
	
	construct( vec3 pos, player owner, angle xyAngle, int lvl )
		super(pos, owner, xyAngle, SPEAR_FX )
		
		setRanged(getSpearRange(lvl))
		setSpeed(getSpearSpeed(lvl))
		
		fx.getDummy().registerOnHitCallback(32, true, (unit hit, unit hitter) -> begin
			if fx.getDummy() == hitter
				if hit.isAliveTrick() and hit.getEntity() instanceof Creep  
					var affected = false
					print("check")
					//Checking if not targeted here
					for t in targets
						if t.creep.actor == hit
							affected = true
							print("already existing")
							break
					if not affected
						print("not affected")
						var c = hit.getEntity() castTo Creep
						fx.getDummy().damageTarget(hit, getSpearDamage(lvl))
						print("damage")
						// Todo MASS handling
						if targets.getSize() < getSpearTargetCount(lvl)
							//If everything fine, then add.
							targets.add(new SpearCreep(c, c.getPos2()-getPos2()))
							c.active = false
							c.actor..setTurnSpeed(0)..setPropWindow(0)
		end)
		
		
	override function update()
		for screep in targets
			if screep.creep.actor.isAliveTrick()
				screep.durPercent *= screep.creep.weightFactor
				print("dur: " + screep.durPercent.toString())
				if screep.durPercent < 0.05
					targets.remove(screep)
					screep.creep.active = true
					destroy screep
					screep.creep.actor..setTurnSpeed(0.6)..setPropWindow(80)
				else
					screep.creep.setPos3(getPos3() + vec3((64*screep.durPercent - 32) * xyAngle.cos(),(128*screep.durPercent - 64) * xyAngle.sin(),0) + screep.offset, true)
					screep.creep.actor.setFlyHeight(0,0)
			else
				targets.remove(screep)
				destroy screep
				screep.creep.active = true
		super.update()
		
		
	ondestroy
		fx.getDummy().removeOnHitCallback()
		destroy targets



public function init_Spear(boolean compileTime)
	SPEAR_SPELL_ID = ABIL_ID_GEN.next()
	
	if compileTime
		var def = new ChannelAbilityPreset(SPEAR_SPELL_ID, 4)
		// Set Attributes
		def..removeChannelProperties(true)..setName("Sausage Spear")
			..setButtonPositionNormalX(1)..setButtonPositionNormalY(2)
			..setButtonPositionResearchX(1)..setButtonPositionResearchY(0)
			..setIcon("BTNThoriumRanged")..setOption(Option.VISIBLE, true)
			..setTargetType(Targettype.PUTARGET)
			..setCastRange(1,1000)
			..setOrderStringActivate("starfall")