package Molotov
import Projectile
import PhysicsEntity
import Terrain
import MapStuff
import ChannelAbilityPreset
import ClosureForGroups
import XBurn
import Hero
import Napalm
import Explosive
import AbilityTooltipGenerator

import SoundHelper

constant MOLOTOV_NAME = "Throw Molotov"
constant MOLOTOV_HOTKEY = "E"
constant MOLOTOV_MAXLVL = 4

function getMolotovBounces(int lvl) returns int
	return min(lvl, 3)
	
constant THROW_SOUND = new Sound("Sounds\\sfx_throw.mp3", 532, false, true)
constant EXPLO_SOUND = new Sound("Sounds\\sfx_molotov_explo.mp3", 4021, false, true)
public int MOLOTOV_SPELL_ID

public class Molotov extends Projectile
	use PhysicsModule
	
	int bounces
	int lvl
	Hero caster = null
		
	construct(vec3 pos, vec2 target, Hero caster, int lvl)
		super(pos, caster.owner, pos.angleTo2d(target), "Models\\Molotov.mdx")
		flying = true
		bounces = getMolotovBounces(lvl)
		setTarget(target, 300)
		this.caster = caster
		THROW_SOUND.playOnUnit(caster.actor)
		
	override function update()
		if bounces < 0
			destroy this
		else
			physicsUpdate(this, false)
			super.update()
		

	override function onGroundHit()
		let nrml = getTerrainNormal(pos.x, pos.y, 4).norm()
		if vel.dot(nrml) > 0
			return
			
		var v = vel.project(nrml)
		v *= -1.8
		vel += v
		EXPLO_SOUND.playOnUnit(fx.getDummy())
		fx.flash("Models\\FlameBomb.mdx")
		forUnitsInRange(pos.toVec2(), 128, (unit u) -> begin
			if u.getOwner().isEnemyOf(this.owner)
				u.addXBurn(caster, 1)
		end)
		for n in freshNapalms
			if pos.distToVec2dSquared(n.pos) < IGNITE_DISTANCE_SQ
				n.ignite()
		for e in explosiveEntities
			if pos.distToVec2dSquared(e.pos.toVec2()) < IGNITE_DISTANCE_SQ
				e.explode()
		bounces--


public function init_Molotov(boolean c)
	MOLOTOV_SPELL_ID = ABIL_ID_GEN.next()
	
	if c
		let molotovPreset = new ChannelAbilityPreset(MOLOTOV_SPELL_ID, MOLOTOV_MAXLVL)
		molotovPreset..setName("Molotov")..removeChannelProperties(true)..setOption(Option.VISIBLE, true)
		..setIcon("BTNSunFragment")..setTargetType(Targettype.PTARGET)
		..setCastRange(1, 400)..setButtonPosNormal(2, 2)..setHotkey(MOLOTOV_HOTKEY)
		new AbilityTooltipGenerator(MOLOTOV_NAME, MOLOTOV_HOTKEY, Targettype.PTARGET, MOLOTOV_MAXLVL, "Throws a burning Molotov into the target area.\nThe Molotov bounces.\n" +
		"When the Molotov hits the ground it ignites all Napalm and Xplosives near and burns enemies.")
		..addProperty("Bounces", (int lvl) -> getMolotovBounces(lvl).toString())
		..applyToDef(molotovPreset)
		