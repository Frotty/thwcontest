package Napalm
import Explosive
import Image
import ChannelAbilityPreset
import MapStuff
import ClosureTimers
import Hero
import initlater XBurn
import ClosureForGroups

constant NAPALM_DURATION = 4
constant NAPALM_BURN_DURATION = 5

public constant IGNITE_DISTANCE_SQ = 75*75

public int NAPALM_SPELL_ID

public LinkedList<NapalmDroplet> freshNapalms = new LinkedList<NapalmDroplet>()
public LinkedList<NapalmDroplet> burningNapalms = new LinkedList<NapalmDroplet>()

public class NapalmDroplet
	Hero caster
	vec2 pos
	effect fireSfx = null
	image img
	unit dummy
	int lvl
	colorA col = colorA(255,255,255,0)
	
	construct(vec2 pos, Hero caster, int lvl)
		this.pos = pos
		this.caster = caster
		var path = "Textures\\NapalmDrop"
		this.lvl = lvl
		path += GetRandomInt(0, 7).toString()
		path += ".blp"
		img = createImage(path, pos, 64, 64)..show()..setLevel(Layer.L4)
		freshNapalms.add(this)
		
	function ignite()
		fireSfx = addEffect(firePaths[GetRandomInt(0, firePathCount)], pos)
		burningNapalms.add(this)
		freshNapalms.remove(this)
		
		doPeriodicallyTimed(0.25, 10, (CallbackCounted cb) -> begin
			col.alpha = (255 * (cb.getCount()/40)).toInt()
			img.setColor(col)
			forUnitsInRange(pos, 64, (unit u) -> begin
				if u.hasXBurn()
					u.refreshXBurn()
				else
					u.addXBurn(caster, 1)
			end)
			for n in freshNapalms
				if pos.distToVecSquared(n.pos) < IGNITE_DISTANCE_SQ
					n.ignite()					
			for e in explosiveEntities
				if pos.distToVecSquared(e.getPos2d()) < IGNITE_DISTANCE_SQ
					e.explode()
					
			if cb.isLast()
				destroy this
		end)
		
	ondestroy
		fireSfx.destr()
		img.remove()
		burningNapalms.remove(this)
		
	
public function init_Napalm(boolean c)
	NAPALM_SPELL_ID = ABIL_ID_GEN.next()
	if c
		let spreset = new ChannelAbilityPreset(NAPALM_SPELL_ID, 4)
		// Set Attributes
		spreset..removeChannelProperties(true)..setName("Napalm")..setButtonPosNormal(1,2)
			..setIcon("BTNFieryExplosion")..setOption(Option.VISIBLE, true)..setHotkey("W")
			
		
string array firePaths
int firePathCount = 5

init
	firePaths[0] = "Environment\\LargeBuildingFire\\LargeBuildingFire1.mdl" 
	firePaths[1] = "Environment\\LargeBuildingFire\\LargeBuildingFire2.mdl" 
	firePaths[2] = "Environment\\SmallBuildingFire\\SmallBuildingFire2.mdl" 
	firePaths[3] ="Environment\\NightElfBuildingFire\\ElfLargeBuildingFire1.mdl" 
	firePaths[4] = "Environment\\NightElfBuildingFire\\ElfLargeBuildingFire2.mdl" 
	firePaths[5] = "Environment\\UndeadBuildingFire\\UndeadLargeBuildingFire2.mdl" 
