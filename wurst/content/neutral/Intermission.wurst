package Intermission
import GameConstants
import MapStuff
import UnitObjEditing
import Fx
import ClosureTimers
import LinkedList
import initlater WaveManager
import initlater Hero

int SHOP_ID

unit SHOP_UNIT
timerdialog td
LinkedList<int> pauseItemsIds = new LinkedList<int>()
LinkedList<Hero> fallenHeros = new LinkedList<Hero>()

public function startIntermission(real duration, string text)
	let fx = new Fx(STATUE_POS.withZ(300), angle(0))
	let t = getTimer()..start(duration, () -> begin
		DestroyTimerDialog(td)
		GetExpiredTimer().release()
	end)
	for h in fallenHeros
		h.actor.revive(STATUE_POS.polarOffset(angle(GetRandomReal(0, PI2)), 100), true)
		Hero.aliveCount++
		h.deathTT.destr()
		destroy h.crossFx
	td = CreateTimerDialog(t)
	TimerDialogDisplay(td, true)
	TimerDialogSetTitle(td, "Intermission")
	fx.setScale(2.)
	fx.flash("Abilities\\Spells\\Undead\\ReplenishHealth\\ReplenishHealthCasterOverhead.mdl")
	doPeriodicallyTimed(3, duration, (CallbackCounted cc) -> begin
		fx.flash("Abilities\\Spells\\Undead\\ReplenishHealth\\ReplenishHealthCasterOverhead.mdl")
	end)
	printTimed("|cff086BAD>> |cffFFD64A" + text + "|r", duration-4)
	printTimed("            |cffFFD64AYou can now regenerate and buy permanent items from the statue.", duration-4)
	SHOP_UNIT..addAbility('ACnr')..addAbility('ANre')
	
	for i in pauseItemsIds
		AddItemToStock(SHOP_UNIT, i, 1, 2)
		
	doAfter(duration, () -> begin
		for i in pauseItemsIds
			RemoveItemFromStock(SHOP_UNIT, i)
		SHOP_UNIT..removeAbility('ACnr')..removeAbility('ANre')
		WaveManager.startNextWave()
	end)
	
	
public function addShopItem(int id, int currentStack, int maxStack, boolean always)
	if always
		AddItemToStock(SHOP_UNIT, id, currentStack, maxStack)
	else
		pauseItemsIds.add(id)
	
	
public function init_Intermission(boolean c)
	SHOP_ID = UNIT_ID_GEN.next()
	if c
		let def = new BuildingDefinition(SHOP_ID, 'ngme')
		def..setName("Statue")..setModelFile("Models\\Statue03_b.mdx")..setItemsSold("")..setNormalAbilities("Asid, Aneu")
		..setPathingMap("")..setGroundTexture("")..setCollisionSize(0)..setSelectionScale(0.8)..setScalingValue(1.5)
	else
		SHOP_UNIT = createUnit(DUMMY_PLAYER, SHOP_ID, STATUE_POS, angle(0))
	